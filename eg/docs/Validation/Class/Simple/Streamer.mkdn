# NAME

Validation::Class::Simple::Streamer - Simple Streaming Data Validation

# VERSION

version 7.900040

# SYNOPSIS

    use Validation::Class::Simple::Streamer;

    my $parameters = {
        credit_card   => '0000000000000000',
        email_address => 'root@localhost',

    };

    my $rules = Validation::Class::Simple::Streamer->new($parameters);

    # the point here is expressiveness
    # directive methods auto-validate in boolean context !!!

    if (not $rules->check('credit_card')->creditcard(['visa', 'mastercard'])) {
        # credit card is invalid visa/mastercard
        warn $rules->messages;
    }

    if (not $rules->check('email_address')->min_length(3)->email) {
        # email address is invalid
        warn $rules->messages;
    }

    # prepare password for validation
    $rules->check('password');

    die "Password is not valid"
        unless $rules->min_symbols(1) && $rules->matches('password2');

    # are you of legal age?
    if ($rules->check('member_years_of_age')->between('18-75')) {
        # access to explicit content approved
    }

    # get all fields with errors
    my $fields = $rules->validator->error_fields;

    # warn with errors if any
    warn $rules->messages unless $rules->is_valid;

    # validate like a boss
    # THE END

# DESCRIPTION

Validation::Class::Simple::Streamer is a simple streaming validation module
that makes data validation fun. Target parameters and attach matching fields
and directives to them by chaining together methods which represent
Validation::Class [directives](http://search.cpan.org/perldoc?Validation::Class::Directives#DIRECTIVES). This
module is built around the powerful [Validation::Class](http://search.cpan.org/perldoc?Validation::Class) data validation
framework via [Validation::Class::Simple](http://search.cpan.org/perldoc?Validation::Class::Simple). This module was inspired by the
simplicity and expressiveness of the Node.js validator library, but built on
top of the ever-awesome Validation::Class framework, which is designed to be
modular and extensible, i.e. whatever custom directives you create and install
will become methods on this class which you can then use to enforce policies.

# RATIONALE

If you are new to Validation::Class, or would like more information on
the underpinnings of this library and how it views and approaches
data validation, please review [Validation::Class::Whitepaper](http://search.cpan.org/perldoc?Validation::Class::Whitepaper).
Please review the ["GUIDED-TOUR" in Validation::Class::Simple](http://search.cpan.org/perldoc?Validation::Class::Simple#GUIDED-TOUR) for a detailed
step-by-step look into how Validation::Class works.

# METHODS

## check

The check method specifies the parameter to be affected by directive methods
if/when called.

    $self = $self->check('email_address'); # focus on email_address

    $self->required;        # apply the Required directive to email_address
    $self->min_symbols(1);  # apply the MinSymbols directive to email_address
    $self->min_length(5);   # apply the MinLength directive to email_address

## clear

The clear method resets the validation queue and declared fields but leaves the
declared parameters in-tact, almost like the object state post-instantiation.

    $self->clear;

## messages

The messages method returns any registered errors as a concatenated string using
the ["errors\_to\_string" in Validation::Class::Prototype](http://search.cpan.org/perldoc?Validation::Class::Prototype#errors\_to\_string) method and accepts the same
parameters.

    print $self->messages("\n");

## params

The params method gives you access to the validator's params list which is a
[Validation::Class::Mapping](http://search.cpan.org/perldoc?Validation::Class::Mapping) object.

    $params = $self->params($parameters);

## validate

The validate method uses the validator to perform data validation based on
the series and sequence of commands issued previously. This method is called
implicitly whenever the object is used in boolean context, e.g. in a conditional.

    $true = $self->validate;

## validator

The validator method gives you access to the object's validation class which is
a [Validation::Class::Simple](http://search.cpan.org/perldoc?Validation::Class::Simple) object by default.

    $validator = $self->validator;

# AUTHOR

Al Newkirk <anewkirk@ana.io>

# COPYRIGHT AND LICENSE

This software is copyright (c) 2011 by Al Newkirk.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.
