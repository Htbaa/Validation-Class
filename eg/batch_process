#!/usr/bin/env perl

BEGIN {

use FindBin;
use lib $FindBin::Bin . "/../lib";

}

# handling input for batch-processing

package BatchProcessing;

use Validation::Class;

mixin scrub     => {
    required    => 1,
    filters     => ['trim', 'strip']
};

field header    => {
    mixin       => 'scrub',
    options     => ['name', 'email', 'contact', 'dob', 'country'],
    multiples   => 1
};

field name      => {
    mixin       => 'scrub',
    filters     => ['titlecase'],
    min_length  => 2
};

field email     => {
    mixin       => 'scrub',
    min_length  => 3
};

field contact   => {
    mixin       => 'scrub',
    length      => 10
};

field dob       => {
    mixin       => 'scrub',
    length      => 8,
    pattern     => '##/##/##'
};

field country   => {
    mixin       => 'scrub'
};

package main;

use Data::Dumper;

my $params = {
    pasted_data => <<'PASTE',
name	email	contact	dob	country
john	john@zuzu.com	9849688899	12/05/98	UK
jim kathy	kjim@zuz.com	8788888888	05/07/99	India
Federar	fed@zuzu.com	4484848989	11/21/80	USA
Micheal	micheal@zuzu.com	6665551212	06/10/87	USA
Kwang Kit	kwang@zuzu.com	7775551212	07/09/91	India
Martin	jmartin@zuzu.com	2159995959	02/06/85	India
Roheeth	roheeth@zuzu.com	9596012020	01/10/89	USA
PASTE
};

# ... there are many ways this could be parsed and validated
# ... but this is simple

my $bpi = my @pasted_lines = split /\n/, $params->{pasted_data};
my @headers = split /\t/, shift @pasted_lines;

my $bp  = BatchProcessing->new(params => { header => [@headers] });

# validate headers first

if ($bp->validate) {

    $bp->params->clear;

    $bpi--;

    # validate each line, halt on first bad line

    while (my $line = shift @pasted_lines) {

        my @data = split /\t/, $line;

        for (my $i=0; $i<@data; $i++) {

            $bp->params->add($headers[$i], $data[$i]);

        }

        last unless $bp->validate;

        printf "validated %s", Dumper($bp->params->hash);

        $bp->params->clear;

        $bpi--;

    }

}

print ! $bpi ? "OK" : "NOT OK";
print "\n";

1;
